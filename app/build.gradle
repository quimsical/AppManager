// SPDX-License-Identifier: Apache-2.0 AND GPL-3.0-or-later

apply plugin: 'com.android.application'
apply plugin: 'dev.rikka.tools.refine.gradle-plugin'

android {
    compileSdk rootProject.ext.compileSdk

    defaultConfig {
        applicationId 'io.github.muntashirakon.AppManager'
        minSdk rootProject.ext.minSdk
        targetSdk rootProject.ext.targetSdk
        versionCode 417
        versionName "3.1.0-alpha03"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        // Add build time to BuildConfig
        buildConfigField "long", "BUILD_TIME_MILLIS", "${buildTime()}"
    }

    signingConfigs {
        debug {
            storeFile file('dev_keystore.jks')
            storePassword 'kJCp!Bda#PBdN2RLK%yMK@hatq&69E'
            keyPassword 'kJCp!Bda#PBdN2RLK%yMK@hatq&69E'
            keyAlias 'key0'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "App Manager"
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            signingConfig signingConfigs.debug
            resValue "string", "app_name", "AM Debug"
        }
    }
    lint {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
    splits {
        abi {
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true
        }
    }
    androidResources {
        noCompress 'jar', 'sh'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }
}

dependencies {
    compileOnly project(path: ':hiddenapi')

    // Deprecated: This is being replaced
    implementation project(path: ':libapkparser')

    // Core Libraries
    implementation project(path: ':libcore:compat')
    implementation project(path: ':libcore:io')
    implementation project(path: ':libcore:ui')
    implementation project(path: ':libserver')
    implementation project(path: ':docs')

    // ADB
    implementation 'com.github.MuntashirAkon:libadb-android:2.2.0'

    // API
    implementation 'com.github.MuntashirAkon:unapkm-android:1.2'
    implementation project(path: ':libopenpgp')

    // APK Editing
    implementation 'com.github.MuntashirAkon:apksig-android:4.4.0'
    implementation 'com.github.MuntashirAkon:sun-security-android:1.1'
    implementation "org.bouncycastle:bcprov-jdk15on:${bouncycastle_version}"
    implementation "org.bouncycastle:bcpkix-jdk15on:${bouncycastle_version}"
    implementation 'com.github.MuntashirAkon.jadx:jadx-core:5a62a8e625'
    // Replace SLF4J with a placeholder
    configurations {
        all {
            exclude group: 'org.slf4j', module: 'slf4j-api'
        }
    }
    implementation 'com.github.MuntashirAkon.jadx:jadx-dex-input:5a62a8e625'
    // Replace SLF4J with a placeholder
    configurations {
        all {
            exclude group: 'org.slf4j', module: 'slf4j-api'
        }
    }

    // DB
    implementation "androidx.room:room-runtime:${room_version}"
    annotationProcessor "androidx.room:room-compiler:${room_version}"

    // FM
    implementation "com.j256.simplemagic:simplemagic:1.17"

    // Privileged
    implementation "com.github.topjohnwu.libsu:core:${libsu_version}"
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'
    implementation "dev.rikka.tools.refine:runtime:${refine_version}"

    // UI
    implementation "com.google.android.material:material:${material_version}"
    implementation "androidx.appcompat:appcompat:${appcompat_version}"
    // Fix duplicate classes issue in material
    configurations {
        all {
            exclude group: 'androidx.lifecycle', module: 'lifecycle-viewmodel-ktx'
        }
    }
    // AppCompat 1.5.0 still includes the buggy implementation of documentfile library (1.0.0)
    implementation "androidx.documentfile:documentfile:1.0.1"
    implementation "androidx.core:core-splashscreen:${splashscreen_version}"
    implementation 'androidx.webkit:webkit:1.5.0'

    // Utility
    implementation 'com.google.code.gson:gson:2.9.1'

//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'

    // Espresso UI Testing
//    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"
    // Optional if you need to detect intents.
//    androidTestImplementation "com.android.support.test.espresso:espresso-intents:3.0.2"

    // Unit Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.9'
}

preBuild.dependsOn ":server:build"

static def buildTime() {
    return System.currentTimeMillis();
}
